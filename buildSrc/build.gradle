buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
  }
}

allprojects {
  repositories {
    jcenter()
  }
  apply plugin: 'maven'
  apply plugin: 'maven-publish'
  apply plugin: 'groovy'
}


apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def pomConfig = {
  licenses {
    license {
      name "The Apache Software License, Version 2.0"
      url "http://www.apache.org/licenses/LICENSE-2.0.txt"
      distribution "repo"
    }
  }
  developers {
    developer {
      id "jack_1900"
      name "Jack Cho"
      email "jack_1900@163.com"
    }
  }
}

publishing {
  publications {
    MyPublication(MavenPublication) {
      from components.java
      groupId 'me.ele'
      artifactId 'buttercookie-plugin'
      version '0.0.1'

      pom.withXml {
        def root = asNode()
        root.appendNode('description', 'A gradle plugin for publishing to Bintray')
        root.children().last() + pomConfig
      }
    }
  }
}

dependencies {
  compile fileTree(include: ['*.jar'], dir: 'libs')
  compile gradleApi()
  compile localGroovy()
  compile 'com.android.tools.build:gradle:2.1.0'
  compile 'com.github.javaparser:javaparser-core:2.1.0'
}


bintray {
  user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
  key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
  publications = ['MyPublication']
  pkg {
    repo = 'maven'
    name = 'ButterCookie'
    userOrg = 'peacepassion'
    licenses = ['Apache-2.0']
    vcsUrl = 'https://github.com/bintray/gradle-bintray-plugin.git'
    version {
      name = '0.0.1'
      attributes = ['gradle-plugin': 'ButterCookie:me.ele:buttercookie-plugin']
    }
  }
}

